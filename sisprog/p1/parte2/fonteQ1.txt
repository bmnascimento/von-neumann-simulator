;
; ====== PROGRAMA FONTE PARA A PRIMEIRA QUESTÃO DA PROVA  =======
;
      @    100
; inicia contador de letras
START LDA  COMP   ; comprimento do vetor de letras
      MM   CONT   ; inicia o contador do loop
; constrói inicialmente a instrução LDA INIC adiante
; (a ser incrementada a cada execução do loop)
;
IN    LDA  LDA0
      MM   LDA
      LDA  LDA0+1
      MM   LDA+1
;
; executa a instrução construída logo a seguir:
LDA   LDA  INIC   ; pega a próxima letra no vetor de letras
      PD   SAIDA  ; grava-a no arquivo de saída
;
; testa se é a última letra
      LDA  CONT
      -    UM     ; decrementa contador do loop
      MM   CONT
      JZ   FORA   ; se zerar, terminou o loop, se não, prossegue
;
; incrementa o endereço a ser acessado no vetor de letras
      LDA  LDA+1
      +    UM     ; incrementa byte menos significativo
      MM   LDA+1
;
      JZ   INCR   ; se deu zero, vai incrementar o outro byte
      JP   LDA    ; se não, executa mais uma instancia do loop
;
; incrementa o byte mais significativo do endereço a acessar
INCR  LDA  LDA    
      +    UM     ; incrementa byte mais significativo
      MM   LDA
      JP   LDA    ; e vai executar outra instancia do loop
;
; final do loop
FORA  HM   START  ; pára a máquina e volta ao início se for reacionada
;
; ========================================================================
;
; Dispositivos virtuais de entrada e saída
;
; os endereços 3 e 4 são reservados para operarem como registradores de dados 
; da interface dos dispositivos de entrada e saída respectivamente. 
;
; A instrução GD da MVN deposita na posição reservada 3 de memória o byte lido 
; a partir do arquivo texto que simula a fita de entrada.
;
; A instrução PD da MVN extrai da posição reservada 4 de memória o byte 
; a ser transferido para o arquivo texto que simula a fita de saída.
;
      @   3
ENTRADA  K 0         ; 3 = endereço do dispositivo virtual de entrada
      @   4
SAIDA    K 0         ; 4 = endereço do dispositivo virtual de saída
;
; ==========================  ÁREA DE DADOS   ============================
;
      @   200    ; a partir do endereço /200
;
; constantes e variáveis
;
UM    K   1       ; constante 1
CONT  K   0       ; variável CONT iniciada com zero
LDA0  LDA INIC    ; código da instrução LDA INIC
;
; área do texto a ser manipulado
      @   F00    ; este texto está alocado no endereço /F00 e seguintes
COMP  K   5       ; comprimento do texto
INIC  K   "t"     ; primeira letra do texto
      K   "e"
      K   "x"
      K   "t"
      K   "o"     ; última letra do texto
;
; ========================================================================
;
      #    START  ; o  endereço inicial do programa é START.